pragma ComponentBehavior: Bound

import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import QtQuick.Effects
import Quickshell
import Quickshell.Widgets
import qs.modules.theme
import qs.modules.services
import qs.modules.components
import qs.config

StyledRect {
    id: root

    required property var bar
    property string orientation: "horizontal"

    readonly property bool isVertical: orientation === "vertical"
    readonly property bool isIntegrated: (Config.dock?.theme ?? "default") === "integrated"

    readonly property int iconSize: 18
    readonly property int itemSpacing: 2

    visible: (Config.dock?.enabled ?? false) && isIntegrated

    variant: "bg"
    
    property real startRadius: radius
    property real endRadius: radius

    topLeftRadius: startRadius
    topRightRadius: isVertical ? startRadius : endRadius
    bottomLeftRadius: isVertical ? endRadius : startRadius
    bottomRightRadius: endRadius
    
    enableShadow: Config.showBackground

    implicitWidth: isVertical ? 36 : dockLayout.implicitWidth + 8
    implicitHeight: isVertical ? dockLayoutVertical.implicitHeight + 8 : 36
    
    Layout.maximumWidth: isVertical ? 36 : -1
    Layout.maximumHeight: isVertical ? -1 : 36

    Flickable {
        id: flickable
        anchors.fill: parent
        contentWidth: isVertical ? parent.width : contentContainerHorizontal.width
        contentHeight: isVertical ? contentContainerVertical.height : parent.height
        clip: true
        interactive: true
        boundsBehavior: Flickable.StopAtBounds

        Item {
            id: contentContainerHorizontal
            visible: !root.isVertical
            height: parent.height
            width: Math.max(flickable.width, dockLayout.implicitWidth + 8)

            RowLayout {
                id: dockLayout
                anchors.centerIn: parent
                spacing: root.itemSpacing

                Repeater {
                    model: TaskbarApps.apps

                    IntegratedDockAppButton {
                        required property var modelData
                        appToplevel: modelData
                        iconSize: root.iconSize
                        Layout.alignment: Qt.AlignVCenter
                        orientation: root.orientation
                    }
                }
            }
        }

        Item {
            id: contentContainerVertical
            visible: root.isVertical
            width: parent.width
            height: Math.max(flickable.height, dockLayoutVertical.implicitHeight + 8)

            ColumnLayout {
                id: dockLayoutVertical
                anchors.centerIn: parent
                spacing: root.itemSpacing

                Repeater {
                    model: TaskbarApps.apps

                    IntegratedDockAppButton {
                        required property var modelData
                        appToplevel: modelData
                        iconSize: root.iconSize
                        Layout.alignment: Qt.AlignHCenter
                        orientation: root.orientation
                    }
                }
            }
        }
    }
}
